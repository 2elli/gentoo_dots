# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# -------------------
# zimfw configuration
# -------------------

# Use degit instead of git as the default tool to install and update modules.
zstyle ':zim:zmodule' use 'degit'

# autosuggestions
ZSH_AUTOSUGGEST_MANUAL_REBIND=1
ZSH_AUTOSUGGEST_STRATEGY=(completion)

# zsh-syntax-highlighting
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

##################
### zsh config ###
##################

##### opts
setopt EXTENDED_GLOB
setopt NO_CLOBBER
setopt INTERACTIVE_COMMENTS

setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE

##### history
HISTSIZE=500
SAVEHIST=0
unset HISTFILE

##### input
bindkey -v
bindkey '^Y' autosuggest-accept

# Remove path separator from WORDCHARS.
WORDCHARS=${WORDCHARS//[\/]}

##### path
path=( ~/.local/bin $path )
export PATH=$PATH

##### env vars
export EDITOR=nvim
export SYSTEM_EDITOR=nvim
export TERMINAL=alacritty

##### alias
alias nvimc='nvim --clean'
alias tree='lsd --tree'
alias wlc="wl-copy"
alias wlp="wl-paste"

##### functions
function clear_clip {
    :|wl-copy
}

# clear zsh hist
function clear_history {
    if [[ -n $HISTFILE ]]; then
        :>!"$HISTFILE"
    fi
    history -p
    clear
}

# clear evalcache
function clear_evalcache {
    if [[ -n $ZSH_EVALCACHE_DIR ]]; then
        setopt localoptions rmstarsilent
        rm -f "$ZSH_EVALCACHE_DIR"/*
    fi
}

# clear clipboard and history
function clean {
    clear_evalcache
    clear_clip
    clear_history
}

# clear nvim undodir
function clean_nvim_undodir {
    NVIMUNDODIR=~/.local/share/nvim/undodir
    if [[ ! -d "$NVIMUNDODIR" ]]; then
        echo "$NVIMUNDODIR" does not exist
        return 1
    fi
    echo "deleting undo files older than 3 days"
    find "$NVIMUNDODIR" -type f -mtime +3 -delete
}


# ------------------
# Initialize modules
# ------------------

ZIM_HOME=${ZDOTDIR:-${HOME}}/.zim
# Download zimfw plugin manager if missing.
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
    if (( ${+commands[curl]} )); then
        curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
            https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
    else
        mkdir -p ${ZIM_HOME} && wget -nv -O ${ZIM_HOME}/zimfw.zsh \
            https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
    fi
fi
# Install missing modules, and update ${ZIM_HOME}/init.zsh if missing or outdated.
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZIM_CONFIG_FILE:-${ZDOTDIR:-${HOME}}/.zimrc} ]]; then
    source ${ZIM_HOME}/zimfw.zsh init
fi
# Initialize modules.
source ${ZIM_HOME}/init.zsh

# ------------------------------
# Post-init module configuration
# ------------------------------

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
